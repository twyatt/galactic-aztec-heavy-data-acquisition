apply plugin: 'java'

/**
 * Using javafx-gradle-plugin enhances your build-script with javapackager-power.
 *
 * https://github.com/FibreFoX/javafx-gradle-plugin
 */
apply plugin: 'javafx-gradle-plugin'

/**
 * Generate version name using Git tag
 *
 * @see <a href="http://ryanharter.com/blog/2013/07/30/automatic-versioning-with-git-and-gradle/">
 *     Automatic Versioning With Git and Gradle</a>
 */
def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

repositories {
    mavenCentral()
}

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'de.dynamicfiles.projects.gradle.plugins:javafx-gradle-plugin:8.7.0'
    }
}

jfx {
    mainClass 'client.Launcher'
    appName 'DataAcquisitionClientPhidgets'
    vendor 'SDSU Rocket Project'
    jfxMainAppJarName 'data-acquisition-client-phidgets.jar'
    nativeReleaseVersion getVersionName().replace("-phidgets", "")
    verbose true

    bundleArguments = [
        'jnlp.outfile': 'data-acquisition-client-phidgets'
    ]
}

dependencies {
    compile project(':core')
    compile fileTree(dir: 'libs', include: '*.jar')
}
